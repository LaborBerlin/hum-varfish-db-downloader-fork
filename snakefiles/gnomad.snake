# Download gnomAD exomes and genomes, normalize, and convert into TSV for import.


rule grch37_gnomad_v2_1_download:
    output:
        vcf="GRCh37/gnomAD_{kind}/r2.1/download/gnomad.{kind}.r2.1.sites.chr{chrom}.vcf.bgz",
        tbi="GRCh37/gnomAD_{kind}/r2.1/download/gnomad.{kind}.r2.1.sites.chr{chrom}.vcf.bgz.tbi",
    log: "GRCh37/gnomAD_{kind}/r2.1/download/gnomad.{kind}.r2.1.sites.chr{chrom}.vcf.bgz.log"
    shell:
        r"""
        wget \
            -o {log} \
            -O {output.vcf} \
            https://storage.googleapis.com/gnomad-public/release/2.1/vcf/{wildcards.kind}/gnomad.{wildcards.kind}.r2.1.sites.chr{wildcards.chrom}.vcf.bgz
        wget \
            -o {log} \
            -O {output.tbi} \
            https://storage.googleapis.com/gnomad-public/release/2.1/vcf/{wildcards.kind}/gnomad.{wildcards.kind}.r2.1.sites.chr{wildcards.chrom}.vcf.bgz.tbi

        pushd $(dirname {output.vcf})
        md5sum $(basename {output.vcf}) >$(basename {output.vcf}).md5
        md5sum $(basename {output.tbi}) >$(basename {output.tbi}).md5
        """


rule grch37_gnomad_v2_1_normalize:
    input:
        reference="GRCh37/reference/hs37d5/hs37d5.fa",
        vcf="GRCh37/gnomAD_{kind}/r2.1/download/gnomad.{kind}.r2.1.sites.chr{chrom}.vcf.bgz",
    output:
        vcf="GRCh37/gnomAD_{kind}/r2.1/download/gnomad.{kind}.r2.1.sites.chr{chrom}.normalized.vcf.bgz",
        tbi="GRCh37/gnomAD_{kind}/r2.1/download/gnomad.{kind}.r2.1.sites.chr{chrom}.normalized.vcf.bgz.tbi",
    shell:
        r"""
        bcftools norm \
            --threads 4 \
            --multiallelics - \
            --fasta-ref {input.reference} \
            -O z \
            -o {output.vcf} \
            {input.vcf}
        tabix -f {output.vcf}

        pushd $(dirname {output.vcf})
        md5sum $(basename {output.vcf}) >$(basename {output.vcf}).md5
        """


ruleorder: grch37_gnomad_v2_1_normalize > grch37_gnomad_v2_1_download


# Rule to generate TSV files from chromosomes
## Note: From the release notes (https://macarthurlab.org/2018/10/17/gnomad-v2-1/)
## Hemizygote counts
## For this release, there are no more hemizygote count annotations. Instead, our
## male counts correctly consider males as hemizygous in non-pseudo-autosomal (non-PAR)
## regions of the sex chromosomes and we provide a nonpar flag to identify all variants that are in the these regions.
rule grch37_gnomad_exomes_v2_1_to_tsv:
    input:
        vcf="GRCh37/gnomAD_exomes/r2.1/download/gnomad.exomes.r2.1.sites.chr{chrom}.normalized.vcf.bgz",
        header="header/gnomad_exomes.txt",
    output:
        tsv="GRCh37/gnomAD_exomes/r2.1/GnomadExomes.{chrom}.tsv",
        release_info="GRCh37/gnomAD_exomes/r2.1/GnomadExomes.{chrom}.release_info",
    shell:
        r"""
        (
            cat {input.header} | tr '\n' '\t' | sed -e 's/\t*$/\n/g';
            bcftools query {input.vcf} \
                -f 'GRCh37\t%CHROM\t%POS\t%REF\t%ALT\t%AC\t%AC_afr\t%AC_amr\t%AC_asj\t%AC_eas\t%AC_fin\t%AC_nfe\t%AC_oth\t%AC_sas\t%AN\t%AN_afr\t%AN_amr\t%AN_asj\t%AN_eas\t%AN_fin\t%AN_nfe\t%AN_oth\t%AN_sas\t%AF\t%AF_afr\t%AF_amr\t%AF_asj\t%AF_eas\t%AF_fin\t%AF_nfe\t%AF_oth\t%AF_sas\t%AC_male\t%AC_afr_male\t%AC_amr_male\t%AC_asj_male\t%AC_eas_male\t%AC_fin_male\t%AC_nfe_male\t%AC_oth_male\t%AC_sas_male\t%nhomalt\t%nhomalt_afr\t%nhomalt_amr\t%nhomalt_asj\t%nhomalt_eas\t%nhomalt_fin\t%nhomalt_nfe\t%nhomalt_oth\t%nhomalt_sas\t%popmax\t%AC_popmax\t%AN_popmax\t%AF_popmax\t.\t%nhomalt_popmax\t%nonpar\n' \
            | sort -u -k 2,5 -S 80% \
            | awk -F $'\t' \
                'BEGIN {{
                    OFS = FS
                    nonpar = 57
                }}
                {{
                    # nonpar is a flag. it will take either "." if not set or "1" if set.
                    if ($nonpar == ".") {{
                        # hemi values are ranging from 33..41
                        for (i=33; i<42; ++i) {{
                            $i = "."
                        }}
                    }}
                    print $0
                }}' \
            | sed -e 's/\t.$//'
        ) > {output.tsv}

        echo -e "table\tversion\tgenomebuild\tnull_value\nGnomadExomes\tr2.1\tGRCh37\t." > {output.release_info}
        """


# Rule to generate TSV files from chromosomes
rule grch37_gnomad_genomes_v2_1_to_tsv:
    input:
        vcf="GRCh37/gnomAD_genomes/r2.1/download/gnomad.genomes.r2.1.sites.chr{chrom}.normalized.vcf.bgz",
        header="header/gnomad_genomes.txt",
    output:
        tsv="GRCh37/gnomAD_genomes/r2.1/GnomadGenomes.{chrom}.tsv",
        release_info="GRCh37/gnomAD_genomes/r2.1/GnomadGenomes.{chrom}.release_info",
    shell:
        r"""
        (
            cat {input.header} | tr '\n' '\t' | sed -e 's/\t*$/\n/g';
            bcftools query {input.vcf} \
                -f 'GRCh37\t%CHROM\t%POS\t%REF\t%ALT\t%AC\t%AC_afr\t%AC_amr\t%AC_asj\t%AC_eas\t%AC_fin\t%AC_nfe\t%AC_oth\t%AN\t%AN_afr\t%AN_amr\t%AN_asj\t%AN_eas\t%AN_fin\t%AN_nfe\t%AN_oth\t%AF\t%AF_afr\t%AF_amr\t%AF_asj\t%AF_eas\t%AF_fin\t%AF_nfe\t%AF_oth\t%AC_male\t%AC_afr_male\t%AC_amr_male\t%AC_asj_male\t%AC_eas_male\t%AC_fin_male\t%AC_nfe_male\t%AC_oth_male\t%nhomalt\t%nhomalt_afr\t%nhomalt_amr\t%nhomalt_asj\t%nhomalt_eas\t%nhomalt_fin\t%nhomalt_nfe\t%nhomalt_oth\t%popmax\t%AC_popmax\t%AN_popmax\t%AF_popmax\t.\t%nhomalt_popmax\t%nonpar\n' \
            | sort -u -k 2,5 -S 80% \
            | awk -F $'\t' \
                'BEGIN {{
                    OFS = FS
                    nonpar = 52
                }}
                {{
                    # nonpar is a flag. it will take either "." if not set or "1" if set.
                    if ($nonpar == ".") {{
                        # hemi values are ranging from 30..37
                        for (i=30; i<38; ++i) {{
                            $i = "."
                        }}
                    }}
                    print $0
                }}' \
            | sed -e 's/\t.$//'
        ) > {output.tsv}

        echo -e "table\tversion\tgenomebuild\tnull_value\nGnomadGenomes\tr2.1\tGRCh37\t." > {output.release_info}
        """
