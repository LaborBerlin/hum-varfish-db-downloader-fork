# Obtain "knownGeneAA" track from ENSEMBL and convert into TSV via VCF.


rule grch37_knowngeneaa_download:
    output:
        fa="GRCh37/knowngeneaa/latest/download/knownGene.exonAA.fa.gz",
    shell:
        r"""
        wget \
            -O {output.fa} \
            http://hgdownload.cse.ucsc.edu/goldenpath/hg19/multiz100way/alignments/knownGene.exonAA.fa.gz
        """


rule grch37_knowngeneaa_to_vcf:
    input:
        reference="GRCh37/reference/hs37d5/hs37d5.fa",
        fa="GRCh37/knowngeneaa/latest/download/knownGene.exonAA.fa.gz",
    output:
        vcf="GRCh37/knowngeneaa/latest/knownGeneAA.vcf.gz",
        tbi="GRCh37/knowngeneaa/latest/knownGeneAA.vcf.gz.tbi",
    shell:
        r"""
        python tools/knowngeneaa.py \
            {input.reference} \
            {input.fa} \
            --output /dev/stdout \
        | bcftools sort \
            -O z \
            -o {output.vcf}
        tabix -f {output.vcf}
        """


rule grch37_knowngeneaa_to_tsv:
    input:
        header="header/knowngeneaa.txt",
        vcf="GRCh37/knowngeneaa/latest/knownGeneAA.vcf.gz",
    output:
        tsv="GRCh37/knowngeneaa/latest/KnowngeneAA.tsv",
        release_info="GRCh37/knowngeneaa/latest/KnowngeneAA.release_info",
    shell:
        r"""
        (
            cat {input.header} | tr '\n' '\t' | sed -e 's/\t*$/\n/g';
            bcftools query \
                -f "%CHROM\t%POS\t%END\t%UCSC_GENE\t%ALIGNMENT\n" \
                {input.vcf}
        ) \
        > {output.tsv}

        echo -e "table\tversion\tgenomebuild\tnull_value\nKnowngeneAA\t$(date +%Y/%m/%d)\tGRCh37\t" > {output.release_info}
        """
