# Download dbSNP, normalize, and convert into TSV for import.

rule grch37_dbsnp_b151_download:
    output:
        vcf="GRCh37/dbSNP/b151/download/All_20180423.vcf.gz",
        tbi="GRCh37/dbSNP/b151/download/All_20180423.vcf.gz.tbi",
    log: "GRCh37/dbSNP/b151/download/All_20180423.vcf.gz.log"
    shell:
        r"""
        wget \
            -o {log} \
            -O {output.vcf} \
            ftp://ftp.ncbi.nih.gov/snp/organisms/human_9606_b151_GRCh37p13/VCF/All_20180423.vcf.gz
        wget \
            -a {log} \
            -O {output.tbi} \
            ftp://ftp.ncbi.nih.gov/snp/organisms/human_9606_b151_GRCh37p13/VCF/All_20180423.vcf.gz.tbi

        pushd $(dirname {output.vcf})
        md5sum $(basename {output.vcf}) >$(basename {output.vcf}).md5
        md5sum $(basename {output.tbi}) >$(basename {output.tbi}).md5
        """

rule grch37_dbsnp_b151_normalize:
    input:
        reference="GRCh37/reference/hs37d5/hs37d5.fa",
        vcf="GRCh37/dbSNP/b151/download/All_20180423.vcf.gz",
    output:
        release_info="GRCh37/dbSNP/b151/dbsnp.release_info",
        vcf="GRCh37/dbSNP/b151/All_20180423.vcf.gz",
        tbi="GRCh37/dbSNP/b151/All_20180423.vcf.gz.tbi",
    shell:
        r"""
        bcftools norm \
            --threads 4 \
            --multiallelics - \
            --fasta-ref {input.reference} \
            -O z \
            -o {output.vcf} \
            {input.vcf}
        tabix -f {output.vcf}

        echo -e "table\tversion\ndbsnp\tb151" > {output.release_info}

        pushd $(dirname {output.vcf})
        md5sum $(basename {output.vcf}) >$(basename {output.vcf}).md5
        """
