# Download dbSNP, normalize, and convert into TSV for import.


rule grch37_dbsnp_b151_download:
    output:
        vcf="GRCh37/dbSNP/b151/download/All_20180423.vcf.gz",
        tbi="GRCh37/dbSNP/b151/download/All_20180423.vcf.gz.tbi",
    log: "GRCh37/dbSNP/b151/download/All_20180423.vcf.gz.log"
    shell:
        r"""
        wget \
            -o {log} \
            -O {output.vcf} \
            ftp://ftp.ncbi.nih.gov/snp/organisms/human_9606_b151_GRCh37p13/VCF/All_20180423.vcf.gz
        wget \
            -a {log} \
            -O {output.tbi} \
            ftp://ftp.ncbi.nih.gov/snp/organisms/human_9606_b151_GRCh37p13/VCF/All_20180423.vcf.gz.tbi

        pushd $(dirname {output.vcf})
        md5sum $(basename {output.vcf}) >$(basename {output.vcf}).md5
        md5sum $(basename {output.tbi}) >$(basename {output.tbi}).md5
        """


rule grch37_dbsnp_b151_normalize:
    input:
        reference="GRCh37/reference/hs37d5/hs37d5.fa",
        vcf="GRCh37/dbSNP/b151/download/All_20180423.vcf.gz",
    output:
        vcf="GRCh37/dbSNP/b151/All_20180423.vcf.gz",
        tbi="GRCh37/dbSNP/b151/All_20180423.vcf.gz.tbi",
    shell:
        r"""
        bcftools norm \
            --threads 16 \
            --multiallelics - \
            --fasta-ref {input.reference} \
            -O z \
            -o {output.vcf} \
            {input.vcf}
        tabix -f {output.vcf}

        pushd $(dirname {output.vcf})
        md5sum $(basename {output.vcf}) >$(basename {output.vcf}).md5
        """


rule grch37_dbsnp_b151_tsv:
    input:
        header="header/dbsnp.txt",
        vcf="GRCh37/dbSNP/b151/All_20180423.vcf.gz",
        tbi="GRCh37/dbSNP/b151/All_20180423.vcf.gz.tbi",
    output:
        release_info="GRCh37/dbSNP/b151/Dbsnp.release_info",
        tsv="GRCh37/dbSNP/b151/Dbsnp.tsv",
    shell:
        r"""
        (
            cat {input.header} | tr '\n' '\t' | sed -e 's/\t*$/\n/g';
            bcftools query {input.vcf} \
                -f 'GRCh37\t%CHROM\t%POS\t%END\t\t%REF\t%ALT\t%ID\n' \
            | sort -u -t $'\t' -k 2,2 -k 3,3 -k 6,6 -k 7,7 -S 80%
        ) \
        | python tools/ucsc_binning.py \
        > {output.tsv}

        echo -e "table\tversion\tgenomebuild\tnull_value\nDbsnp\tb151\tGRCh37\t" > {output.release_info}
        """
