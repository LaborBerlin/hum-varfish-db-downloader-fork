rule grchxx_refseq_to_ensembl_download:
    output:
        "{genome_build}/refseqtoensembl/latest/download/gene2ensembl.gz",
    shell:
        r"""
        wget \
            'ftp://ftp.ncbi.nih.gov/gene/DATA/gene2ensembl.gz' \
            -O {output}
        """


rule grchxx_refseq_to_ensembl_tsv:
    input:
        header="header/refseqtoensembl.txt",
        gz="{genome_build}/refseqtoensembl/latest/download/gene2ensembl.gz"
    output:
        tsv="{genome_build}/refseqtoensembl/latest/RefseqToEnsembl.tsv",
        release_info="{genome_build}/refseqtoensembl/latest/RefseqToEnsembl.release_info"
    shell:
        r"""
        (
            cat {input.header} | tr '\n' '\t' | sed -e 's/\t*$/\n/g';
            gunzip -c {input.gz} \
            | awk -F $"\t" 'BEGIN{{OFS=FS}}$1=="9606"{{split($5,a,".");print $2,$3,a[1]}}'
        ) > {output.tsv}

        echo -e "table\tversion\tgenomebuild\tnull_value\nRefseqToEnsembl\t$(date +%Y/%m/%d)\t{wildcards.genome_build}\t-" > {output.release_info}
        """
