# Build ClinVar TSV files.


# Run clinvar_tsv tool to build for latest clinvar.
rule grch37_clinvar_latest_run_clinvar_tsv:
    input:
        hs37d5="GRCh37/reference/hs37d5/hs37d5.fa",
        hs38="GRCh38/reference/hs38/hs38.fa",
    output:
        b38_single="GRCh37/clinvar/latest/clinvar_tsv_main/output/clinvar_allele_trait_pairs.single.b38.tsv.gz",
        b38_multi="GRCh37/clinvar/latest/clinvar_tsv_main/output/clinvar_allele_trait_pairs.multi.b38.tsv.gz",
        b37_single="GRCh37/clinvar/latest/clinvar_tsv_main/output/clinvar_allele_trait_pairs.single.b37.tsv.gz",
        b37_multi="GRCh37/clinvar/latest/clinvar_tsv_main/output/clinvar_allele_trait_pairs.multi.b37.tsv.gz",
        done=touch("GRCh37/clinvar/latest/clinvar_tsv_main/.done"),
    log: "GRCh37/clinvar/latest/clinvar_tsv_main/clinvar_tsv.log"
    shell:
        r"""
        clinvar_tsv \
            main \
            --b37-path $(readlink -f {input.hs37d5}) \
            --b38-path $(readlink -f {input.hs38}) \
            --work-dir $(dirname {output.done}) \
        &> {log}
        """


# Copy TSV out to appropriate position, create release_info files.
rule grchXX_clinvar_latest_postprocess:
    input:
        single="GRCh37/clinvar/latest/clinvar_tsv_main/output/clinvar_allele_trait_pairs.single.b{release}.tsv.gz",
        multi="GRCh37/clinvar/latest/clinvar_tsv_main/output/clinvar_allele_trait_pairs.multi.b{release}.tsv.gz",
        done="GRCh37/clinvar/latest/clinvar_tsv_main/.done",
    output:
        tsv="GRCh{release}/clinvar/latest/Clinvar.tsv",
        md5="GRCh{release}/clinvar/latest/Clinvar.tsv.md5",
        release_info="GRCh{release}/clinvar/latest/Clinvar.release_info",
    shell:
        r"""
        (
            zcat {input.single};
            zcat {input.multi} | tail -n +2
        ) \
        > {output.tsv}

        pushd $(dirname {output.tsv})
        md5sum $(basename {output.tsv}) >$(basename {output.tsv}).md5
        popd

        echo -e "table\tversion\tgenomebuild\tnull_value\nClinvar\t$(date +%Y/%m/%d)\tGRCh{wildcards.release}\t" > {output.release_info}
        """
